Model:
    imports+=Import*
    packages+=Package+
;
Package:
    "package" name=ID "{"
    (
    packages+=Package
    |
    structs+=Struct
    |
    types+=Type
    )*
    "}"
;
Type: RawType|Struct;
RawType:
    "type" name=ID
;
Struct:
    "struct" name=ID "{"
    attributes+=Attribute*
    "}"
;
Attribute: ScalarAttribute|ArrayAttribute;

ScalarAttribute:
    "scalar" name=ID ":" type=[Type|FQN]
;
ArrayAttribute:
    "array" name=ID ":" type=[Type|FQN] "[" array_size=Sum "]"
;
Sum:
    (summands+=Mul ( "+" summands+=Mul )*)
;
Mul:
    (factors+=Factor ( "*" factors+=Factor )*)
    |
    ("(" factors+=Factor ( "*" factors+=Factor )* ")")
;
Factor:
    value=UINT | ref=ScalarRef | "(" sum=Sum ")"
;

ScalarRef:
    ref0=[ScalarAttribute] ("." ref1=[ScalarAttribute] ("." ref2=[ScalarAttribute] )?)?
;

Import:
    "import" importURI=STRING
;
FQN: ID("." ID)*;
UINT: /\d+/ ;
Comment: /\/\/.*$/;
